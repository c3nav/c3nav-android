apply plugin: 'com.android.application'

ext.versionMajor = 4
ext.versionMinor = 1
ext.versionPatch = 1
ext.minimumSdkVersion = 14

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        applicationId "de.c3nav.droid"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion 29
        versionCode generateVersionCode()
        versionName generateVersionName()
        buildConfigField "String", "WEB_URL", "\"https://36c3.c3nav.de\""
    }
    signingConfigs {
        release {
            storeFile file("../../c3nav.keystore")
            storePassword System.getenv("KSTOREPWD")
            keyAlias "c3nav"
            keyPassword System.getenv("KSTOREPWD")
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            zipAlignEnabled true
        }
    }
    lintOptions {
        disable 'AddJavascriptInterface', 'SetJavaScriptEnabled'
    }
}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 1000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    return "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'com.android.support:support-annotations:28.0.0'
}
